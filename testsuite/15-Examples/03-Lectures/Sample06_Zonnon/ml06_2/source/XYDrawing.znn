(* ********* Zonnon online collection ***********
 * XYDrawing
 *
 * This example is a part of Prof. Jurg Gutknecht's lectures
 * www.zonnon.ethz.ch/usergroup
 * (c) ETH Zurich 
 *)

module XYDrawing; 
 
import 
	System.Threading.Thread as Thread, 
	System.Threading.ThreadStart as ThreadStart, 

	System.Drawing.Size as Size, 
	System.Drawing.Point as Point, 
	System.Drawing.Rectangle as Rectangle, 
	System.Drawing.Color as Color, 
	System.Drawing.Graphics as Graphics,  

	System.Windows.Forms.Control as Control,
	System.Windows.Forms.KeyPressEventHandler as KeyPressEventHandler,
	System.Windows.Forms.KeyPressEventArgs as KeyPressEventArgs,
	System.Windows.Forms.Application as Application,
	System.Windows.Forms.Form as Form;
 
var {public}	
	form_init : boolean;

var {public}
	xsize, ysize : integer; (* Window size. *)
	dbx, dby : integer;
	currentChar : char;

var
	df : DrawingForm;

object {public, ref} DrawingForm implements Form;
	var {public}
		dsize : Size;
		rect  : Rectangle;
		point : Point;
		keyPressEventHandler : KeyPressEventHandler;

	procedure {public} OnPressKey(sender : object; args : KeyPressEventArgs);
	begin
		currentChar := char(args.KeyChar); 
	end OnPressKey;
		
begin (* DrawingForm constructor. *)
	rect.X := dbx;
	rect.Y := dby;
	rect.Width := 0;
	rect.Height := 0;
	DesktopBounds := rect;

	dsize.Width := xsize;
	dsize.Height := ysize;
	ClientSize := dsize;

	Text := "XYDrawing";
	BackColor := Color.White;
	ShowInTaskbar := true;
	AutoScale := false;
	AllowDrop := false;

	form_init := true;
end DrawingForm;

(* Just a helper object, rg. *)
object {public, ref} ThreadStarter;
	procedure {public} Running;
	begin
		df := new DrawingForm();
		Application.Run(df);  
	end Running;
end ThreadStarter;

var  
	sysThreadObj : Thread;
	sysThreadStartObj : ThreadStart;
	st : ThreadStarter;
	
procedure {public} Open(wx, wy : integer; var GraphicObj : Graphics);
begin
	xsize := wx; 
	ysize := wy;

	st := new ThreadStarter();
	sysThreadStartObj := new ThreadStart(st.Running);

	sysThreadObj := new Thread(sysThreadStartObj);
	sysThreadObj.Start();

	while ~form_init do Thread.Sleep(50) end;
	putGraphic(GraphicObj);
end Open;

procedure {public} putGraphic(var GraphicObj : Graphics);
begin
	GraphicObj := df.CreateGraphics();
end putGraphic;
 		
begin
	form_init := false; 
	dbx := 50; dby := 50;
end XYDrawing.